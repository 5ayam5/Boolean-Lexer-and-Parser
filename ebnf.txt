InputFile	::= [program] EOF .
program		::= statement {statement} .
statement	::= formula TERM .
formula		::= IF formula THEN evaluation ELSE formula | evaluation .
evaluation	::= infix IMPLIES evaluation | infix .
infix		::= infix BINOP unary | unary .
unary		::= NOT unary | val .
val			::= CONST | ID | LPAREN formula RPAREN .
BINOP		::= AND | OR | XOR | EQUALS .
CONST		::= TRUE | FALSE .
TERM		::= ";" .
IF			::= "IF" .
THEN		::= "THEN" .
ELSE		::= "ELSE" .
IMPLIES		::= "IMPLIES" .
NOT			::= "NOT" .
LPAREN		::= "(" .
RPAREN		::= ")" .
AND			::= "AND" .
OR			::= "OR" .
XOR			::= "XOR" .
EQUALS		::= "EQUALS" .
TRUE		::= "TRUE" .
FALSE		::= "FALSE" .
ID			::= letter {letter} .
letter		::= lowerCase | upperCase .
lowerCase	::= "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" | "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r" | "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z" .
upperCase	::= "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J" | "K" | "L" | "M" | "N" | "O" | "P" | "Q" | "R" | "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z" .